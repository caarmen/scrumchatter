apply plugin: 'com.android.application'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    }
}

apply plugin: 'com.github.ben-manes.versions'

android {
    File signFile = rootProject.file('signing.properties')
    if (signFile.exists()) {
        Properties p = new Properties();
        p.load(new FileInputStream(signFile))

        signingConfigs {
            release {
                keyAlias p.KeyAlias
                keyPassword p.KeyPassword
                storeFile file(p.StoreFile)
                storePassword p.StorePassword
            }
        }
        android.buildTypes.release.signingConfig =signingConfigs.release
    } else  {
        android.buildTypes.release.signingConfig = null
    }
    compileSdkVersion 27
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion "27.0.3"
    dataBinding {
        enabled = true
    }
    defaultConfig {
        applicationId "ca.rmen.android.scrumchatter"
        versionCode 10605
        versionName "1.6.5"
        minSdkVersion 14
        targetSdkVersion 27
        return true; // Avoid "Not all execution paths return a value"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }
    return true;
}

dependencies {
    implementation 'com.github.lecho:hellocharts-library:1.5.8@aar'
    implementation "com.android.support:appcompat-v7:${rootProject.supportLibVersion}"
    implementation "com.android.support:cardview-v7:${rootProject.supportLibVersion}"
    implementation "com.android.support:design:${rootProject.supportLibVersion}"
    implementation "com.android.support:preference-v7:${rootProject.supportLibVersion}"
    implementation ('net.sourceforge.jexcelapi:jxl:2.6.12') {
        exclude group: "log4j"
    }
    implementation "io.reactivex.rxjava2:rxjava:2.1.12"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.2"
    implementation 'org.apmem.tools:layouts:1.10@aar'
}

// Only show real releases with the ben-manes plugin.
dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'alpha-preview', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}
